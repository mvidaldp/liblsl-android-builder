//
// Uncomment the blocks "externalNativeBuild" for building the app with liblsl (LSL) from the source
// code. Make sure you have liblsl's source code
// (https://github.com/sccn/liblsl/releases/latest -> Source code) on /app-root/liblsl or the path
// you specified on the block "externalNativeBuild.cmake" -> path "./liblsl-path/CMakeLists.txt"

//
// Comment the "externalNativeBuild" blocks for building the app using the compiled native library
// (liblsl.so). Once the build succeeds, the library files (.so) are automatically copied inside
// the folders:
// /project-root/app/src/debug/jniLibs (for a debug build)
// /project-root/app/src/main/jniLibs (for a release build)
// Note: A backup of these prebuilt native libraries is on /project-root/libs/[debug/release]
//
// IMPORTANT to use LSL on your code:
// - Take the the last version of LSL.java:
// https://github.com/labstreaminglayer/liblsl-Java/blob/master/src/edu/ucsd/sccn/LSL.java
// And copy it inside your app module source code folder (don't forget your package import at the
// top -> package your.package.app.name;). E.g.: /app-root/module/src/main/java/path-to-the-source/
// - Include the following permission on your AndroidManifest.xml:
// <uses-permission android:name="android.permission.INTERNET" />
// - Include the jna (Java Native Access) in your dependencies block. E.g.:
// implementation 'net.java.dev.jna:jna:[latest-version]@aar'
//

apply plugin: 'com.android.application'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(file('../keystore.properties')))

android {
    signingConfigs {
        debug {
            storeFile file('key.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
        release {
            storeFile file('key.jks')
            storePassword keystoreProperties['storePassword']
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
        }
    }
    compileSdk 33
    defaultConfig {
        minSdk 24
        versionCode 1
        versionName '1.16.2'
        setProperty('archivesBaseName', "LSLMarkersSenderExample-$versionName")
        ndk {
            // Specifies the ABI configurations of your native
            // libraries Gradle should build and package with your app.
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
//        externalNativeBuild.cmake {
//            arguments "-DANDROID_CPP_FEATURES=rtti exceptions", "-DANDROID_PLATFORM=24"
//            targets "lsl"
//        }
    }
//    externalNativeBuild.cmake {
//        path "../liblsl/CMakeLists.txt"
//    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            signingConfig signingConfigs.debug
        }
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    packagingOptions {
        jniLibs {
            pickFirsts += ['**/lib/**']
        }
        resources {
            pickFirsts += ['**/lib/**']
        }
    }
    buildToolsVersion = '34.0.0'
    ndkVersion '25.2.9519653'
    namespace 'liblsl.android.builder'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'net.java.dev.jna:jna:5.13.0@aar'
}

// copy native libraries to per project location
tasks.register('copyDebugJniLibs', Copy) {
    from '../app/build/intermediates/merged_native_libs/debug/out/lib'
    into '../app/src/debug/jniLibs'
    include('**/*.*')
    exclude 'mips*', 'armeabi'
}
tasks.register('copyReleaseJniLibs', Copy) {
    from '../app/build/intermediates/stripped_native_libs/release/out/lib'
    into '../app/src/main/jniLibs'
    include('**/*.*')
    exclude 'mips*', 'armeabi'
}

// backup native libraries to per project location
tasks.register('backupDebugJniLibs', Copy) {
    from '../app/src/debug/jniLibs'
    into '../libs/debug'
    include('**/*.*')
}
tasks.register('backupReleaseJniLibs', Copy) {
    from '../app/src/main/jniLibs'
    into '../libs/release'
    include('**/*.*')
}

tasks.configureEach { task ->
    if (task.name == 'assembleDebug') task.finalizedBy('copyDebugJniLibs')
    if (task.name == 'assembleRelease') task.finalizedBy('copyReleaseJniLibs')
    if (task.name == 'copyDebugJniLibs') task.finalizedBy('backupDebugJniLibs')
    if (task.name == 'copyReleaseJniLibs') task.finalizedBy('backupReleaseJniLibs')
}